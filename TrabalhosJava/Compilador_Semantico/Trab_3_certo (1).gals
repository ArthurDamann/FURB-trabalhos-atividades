#Options
GenerateScanner = true
GenerateParser = true
Language = Java
ScannerName = Lexico
ParserName = Sintatico
SemanticName = Semantico
ScannerCaseSensitive = true
ScannerTable = Compact
Input = Stream
Parser = LL
#RegularDefinitions
Letra_M: [A-Z]
Letra_m: [a-z]
dig: [0-9]
#Tokens
//id
id: {Letra_m}(({Letra_m}| {dig} | [_]{dig}) | {Letra_M}({Letra_m} | {dig} | [_]{dig}))* {Letra_M}?

// int
int: {dig}+

// float
float: {dig}+ "." {dig}+

// char
char: "\"([n] | [s] | [t])

// string
string: \" ([^\n\"%"\"] | "%" [x] )* \"

// coment. Linha 
: "#" [^\n]*

// coment. Bloco
: "{" ([^}]|\r|\n)* "}"

// characteres de fomataï¿½ï¿½o 
:[\n\t\s]

// PR
pr_bool = id : "bool"
pr_case = id : "case"
pr_char = id : "char"
pr_echo = id : "echo"
pr_do = id : "do"
pr_end = id : "end"
pr_false = id : "false"
pr_float = id : "float"
pr_int = id : "int"
pr_local = id : "local"
pr_module = id : "module"
pr_request = id : "request"
pr_string = id : "string"
pr_switch = id : "switch"
pr_true = id : "true"
pr_until = id : "until"
pr_while = id : "while"

// SE
"&"
"|"
"!"
"=="
"!="
"<"
">"
"+"
"-"
"*"
"/"
","
";"
"="
"("
")"
#NonTerminals
<program>
<declaracao_de_variaveis>
<lista_de_comandos>
<comando>
<atribuicao>
<entrada>
<saida>
<selecao>
<repeticao>
<tipo>
<lista_de_identificadores>
<declaracao_de_variaveis_1>
<expressao>
<lista_de_expressoes>
<selecao_1>
<program_1>
<Lista_de_comandos_1>
<lista_de_identificadores_1>
<repeticao_1>
<lista_de_expressoes_1>
<expressao1> 
<elemento> 
<relacional> 
<relacional1> 
<operador_relacional>
<aritmetica> 
<aritmetica1> 
<termo> 
<termo1> 
<fator> 

#Grammar
//Lucas Gabriel Devigili e Arthur Henrique Damann

<program> ::= #101 pr_module <program_1> <lista_de_comandos> pr_end #102;
<program_1> ::= <declaracao_de_variaveis> | î;

<lista_de_comandos> ::= <comando> ";" <Lista_de_comandos_1>;
<Lista_de_comandos_1> ::= <lista_de_comandos> | î;
<comando> ::= <atribuicao> | <entrada> |  <saida> | <selecao> | <repeticao>;

<declaracao_de_variaveis> ::= pr_local <tipo> #103 <lista_de_identificadores> #104 ";" <declaracao_de_variaveis_1> ;
<declaracao_de_variaveis_1> ::= <tipo> #103 <lista_de_identificadores> #104 ";" <declaracao_de_variaveis_1> | î ;
<tipo> ::= pr_bool | pr_int | pr_float | pr_char | pr_string;
<lista_de_identificadores> ::= id #105 <lista_de_identificadores_1>;
<lista_de_identificadores_1> ::= "," <lista_de_identificadores> | î;

<atribuicao> ::= id #105 "=" <expressao> #106;

<entrada> ::= pr_request "(" <lista_de_identificadores> #107 ")";

<saida> ::= pr_echo "(" <lista_de_expressoes> ")";
<lista_de_expressoes> ::= <expressao> #108 <lista_de_expressoes_1>;
<lista_de_expressoes_1> ::= "," <lista_de_expressoes> | î;

<selecao> ::= #109 pr_switch <expressao>
              pr_case #111 <expressao> #112 <lista_de_comandos> #113 <selecao_1>
              pr_end #110 ;

<selecao_1> ::= pr_case #111 <expressao> #112 <lista_de_comandos> #113 <selecao_1> | î ;


<repeticao> ::= pr_do #114 <lista_de_comandos> <repeticao_1> ;
<repeticao_1> ::= pr_while <expressao> #115
                | pr_until <expressao> #116 ;



<expressao> ::= <elemento> <expressao1> ;
<expressao1> ::= î | "&" <elemento> #117 <expressao1> | "|" <elemento> #118 <expressao1> ;
<elemento> ::= <relacional> | pr_true #119 | pr_false #120 | "!" <elemento> #121 ;
<relacional> ::= <aritmetica> <relacional1> ;
<relacional1> ::= î | <operador_relacional> #122 <aritmetica> #123 ;
<operador_relacional>::= "==" | "!=" | "<" | ">" ;
<aritmetica> ::= <termo> <aritmetica1> ;
<aritmetica1> ::= î | "+" <termo> #124 <aritmetica1> | "-" <termo> #125 <aritmetica1> ;
<termo> ::= <fator> <termo1> ;
<termo1> ::= î | "*" <fator> #126 <termo1> | "/" <fator> #127 <termo1> ;
<fator> ::= id #128 | int #129 | float #130 | char #131 | string #132 | "(" <expressao> ")" | "+" <fator> | "-" <fator> #133 ;
